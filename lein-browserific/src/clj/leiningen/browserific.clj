(ns leiningen.browserific
  (:require [browserific.builds.chenex :as cb]
            [browserific.builds.cljsbuild :as cljsbuild]
            [browserific.config :as conf]
            [browserific.config.server :as server]
            [browserific.helpers.utils :as u]
            [clojure.java.browse :as b]
            [clojure.java.io :as io]
            [greenyouse.chenex.core :as cc]
            [leiningen.core.main :as lmain]
            [leiningen.help :as lhelp]
            [me.raynes.fs :as fs]
            [plugin-helpers.core :as p])
  (:refer-clojure :exclude [compile])
  (:import java.io.File))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Helper fns

(defn- write-dirs []
  (doseq [plat u/platforms]
    (.mkdirs (File. (str "target/intermediate/" plat)))))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Leiningen fns

(defn- build
  "Write lein-cljsbuilds for all relevant platforms"
  ([]
   (let [{:keys [draft builds] :as opts} u/project-opts]
     (p/info "Writing a new lein-cljsbuild configuration.\n")
     (cond
       (and draft (not (contains? u/platforms draft))) ;invalid draft platform
       (p/abort (str "Browserific Error: " draft
                  " is not a valid platform.\n\nOptions are:\n\n") u/platforms)
       (empty? draft)
       (do (p/warning "Warning: no draft platform specified but building config anyway.\n")
           (fs/delete-dir "builds")
           (cljsbuild/write-builds opts)
           (cb/write-chenex-builds))
       :else
       (do (fs/delete-dir "builds")
           (cljsbuild/write-builds opts)
           (cb/write-chenex-builds draft))))))

(defn- compile
  "Compile the browserific files"
  [project]
  (p/info "Compiling Browserific files.\n")
  (fs/delete-dir "target/intermediate")
  (write-dirs)
  (conf/build-configs)
  (cc/run (get-in project [:chenex :builds])))

(defn- clean
  "Remove files previously generated by browserific, does not remove config files"
  []
  (p/info "Deleting files generated by Browserific.\n")
  (fs/delete-dir "target/intermediate")
  (fs/delete-dir "builds"))

(defn- sample
  "Shows a sample config.edn for either extension, mobile, or desktop.
   If no platform is specified, a sample with all three platforms will be
   shown."
  [& type]
  (case (first type)
    "extension" (-> (io/resource "samples/extension-sample.edn") slurp lmain/info)
    "mobile" (-> (io/resource "samples/mobile-sample.edn") slurp lmain/info)
    "desktop" (-> (io/resource "samples/desktop-sample.edn") slurp lmain/info)
    (-> (io/resource "samples/all-samples.edn") slurp lmain/info)))

(defn- config
  "Launch a server over port 4242 that has a GUI for building a config.edn file"
  []
  (p/info "Starting a new browserific config server on port 4242.")
  (future (Thread/sleep 2000)
          (b/browse-url "http://localhost:4242"))
  (server/config-server))

(defn browserific
  "Run lein-browserific"
  {:help-arglists '([build compile clean sample config])
   :subtasks [#'build #'compile #'clean #'sample #'config]}
  ([project]
     (lmain/info
      (lhelp/help-for "browserific"))
     (lmain/abort))
  ([project subtask & args]
     (case subtask
       "build" (if args (apply build args) (build))
       "compile" (compile project)
       "clean" (clean)
       "sample" (apply sample args)
       "config" (config)
       (do
         (p/warning (str "Browserific Error: Subtask " subtask " not found."))
         (lhelp/subtask-help-for *ns* #'browserific)
         (lmain/abort)))))
